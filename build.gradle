/* 
   Moise Gradle file

*/

defaultTasks 'jar'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version '0.9-SNAPSHOT'
group   'org.jacamo'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    
    maven {
        url "http://jacamo.sourceforge.net/maven2"
    }
    
}

dependencies {
    compile group: 'org.jason-lang', name: 'jason',    version: '2.0-SNAPSHOT', changing: true
    compile group: 'org.jacamo',     name: 'npl' ,     version: '0.2-SNAPSHOT', changing: true
    compile group: 'org.jacamo',     name: 'cartago' , version: '2.0.2'
    compile group: 'org.jacamo',     name: 'c4jason' , version: '2.0.2'
    
    testCompile group: 'junit',      name: 'junit', version: '4.+'
}

jar {
    baseName 'moise'
}

clean {
    delete 'bin'
}

task intsim (type: JavaExec, dependsOn: 'jar') {
    description 'runs an interactive simulation of some organisation'
    main = 'jason.infra.centralised.RunCentralisedMAS'
    args 'org-simulator.mas2j'
    classpath sourceSets.main.runtimeClasspath
    workingDir project.projectDir.absolutePath + '/src/examples/sim' 
}

task sim (type: JavaExec, dependsOn: 'jar') {
    description 'runs a program that simulates some dynamics of an organisation'
    main = 'ora4mas.nopl.simulator.ConsoleSimulator'
    classpath sourceSets.main.runtimeClasspath
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            
            artifactId 'moise'
        }
    }
}

task fixTab {
    ant.fixcrlf(eol: 'lf',  includes: '**/*.txt,**/*.bat, **/*.adoc', srcdir: '.')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml', srcdir: 'src')
}


task publishMavenSF(dependsOn: [ 'build', 'publishToMavenLocal']) << {
    description 'transfer local maven artifact to our maven2 repository at sourceforge'
    
    def wdir = System.getProperty("user.home")+'/.m2/repository/org/jacamo/moise'
    def tdir = 'jomifred,jacamo@web.sf.net:/home/project-web/jacamo/htdocs/maven2/org/jacamo/moise'
    exec {
        commandLine 'scp', '-r', project.version, tdir
        workingDir wdir
    }
    exec {
        commandLine 'cp', 'maven-metadata-local.xml', 'maven-metadata.xml'
        workingDir wdir
    }
    exec {
        commandLine 'scp', 'maven-metadata.xml', tdir
        workingDir wdir
    }
}
