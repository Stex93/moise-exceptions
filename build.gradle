/*
   Moise Gradle file

*/

defaultTasks 'jar'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'

version '0.11-SNAPSHOT'
group   'org.jacamo'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()

    maven {
        url "http://jacamo.sourceforge.net/maven2"
    }

}

configurations {
    umljavadoc  // see http://www.umlgraph.org/doc/indexw.html
}

dependencies {
    //compile group: 'net.sf.jason', name: 'jason',  version: '2.+'
    compile group: 'org.jason-lang', name: 'jason',    version: '2.1', changing: false
    compile group: 'org.jacamo',     name: 'npl' ,     version: '0.3-SNAPSHOT', changing: true, transitive: false
    compile group: 'org.jacamo',     name: 'cartago',  version: '2.3-SNAPSHOT', changing: true
    compile group: 'org.jacamo',     name: 'jaca',     version: '2.3-SNAPSHOT', changing: true

    testCompile group: 'junit',      name: 'junit', version: '4.+'

    umljavadoc 'org.umlgraph:umlgraph:5.6'
}

jar {
    baseName 'moise'
}

clean {
    delete 'bin'
    delete 'test.xml'
}

task intsim (type: JavaExec, dependsOn: 'jar') {
    description 'runs an interactive simulation of some organisation'
    main = 'jason.infra.centralised.RunCentralisedMAS'
    args 'org-simulator.mas2j'
    classpath sourceSets.main.runtimeClasspath
    workingDir project.projectDir.absolutePath + '/examples/sim'
}

task sim (type: JavaExec, dependsOn: 'jar') {
    description 'runs a program that simulates some dynamics of an organisation'
    main = 'ora4mas.nopl.simulator.ConsoleSimulator'
    classpath sourceSets.main.runtimeClasspath
}

eclipse {
    classpath {
        downloadJavadoc = true
    }
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
			artifact sourceJar
			artifact javadocJar
            artifactId 'moise'
        }
    }
}

task fixTab {
    ant.fixcrlf(eol: 'lf',  includes: '**/*.txt,**/*.bat, **/*.adoc', srcdir: '.')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml,**/*.xsl,**/*.xsd', srcdir: 'src')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml,**/*.xsl,**/*.xsd', srcdir: 'examples')
}

task renderAsciiDocs(type: Exec) {
    commandLine 'find', '.', '-name', '*.adoc', '-exec', 'asciidoctor', '{}', ';'
}


task javadoc(overwrite: true, dependsOn: compileJava) {
    setDescription('Generates Javadoc API documentation with UMLGraph diagrams')
    setGroup(JavaBasePlugin.DOCUMENTATION_GROUP)

    doLast {
        ant.javadoc(classpath: (configurations.runtime + configurations.compile).asPath,
                    sourcepath: file('src/main/java'),
                    packagenames: '*',
                    destdir: "${docsDir}/../../doc/api",
                    private: 'true',
                    docletpath: configurations.umljavadoc.asPath) {
            doclet(name: 'org.umlgraph.doclet.UmlGraphDoc') {
                //param(name: '-inferrel')
                //param(name: '-inferdep')
                param(name: '-qualify')
                //param(name: '-T', value: 'svg')
                param(name: '-postfixpackage')
                param(name: '-hide', value: 'java.*')
                //param(name: '-collpackages', value: 'java.util.*')
                //param(name: '-nodefontsize', value: '9')
                //param(name: '-nodefontpackagesize', value: '7')
                //param(name: '-link', value: 'http://java.sun.com/j2se/1.5.0/docs/guide/javadoc/doclet/spec')
                //param(name: '-link', value: 'http://java.sun.com/j2se/1.5/docs/api')
            }
        }
    }
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    baseName 'moise'
    classifier = 'javadoc'
    from '${docsDir}/../../doc/api'
}

task sourceJar (type : Jar) {
    baseName 'moise'
    classifier = 'sources'
    from sourceSets.main.allSource
}


task publishMavenSF(dependsOn: [ 'jar', 'build', 'publishToMavenLocal', 'sourceJar', 'javadocJar']) << {
    description 'transfer local maven artifact to our maven2 repository at sourceforge'

    def wdir = System.getProperty("user.home")+'/.m2/repository/org/jacamo/moise'
    def tdir = 'jomifred,jacamo@web.sf.net:/home/project-web/jacamo/htdocs/maven2/org/jacamo/moise'
    exec {
        commandLine 'scp', '-r', project.version, tdir
        workingDir wdir
    }
    exec {
        commandLine 'cp', 'maven-metadata-local.xml', 'maven-metadata.xml'
        workingDir wdir
    }
    exec {
        commandLine 'scp', 'maven-metadata.xml', tdir
        workingDir wdir
    }
}
